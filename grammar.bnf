<module>            ::= "module" <id> [<declaration> {, <declaration>}];
<declaration>       ::= <open>
                     |  <export>
                     |  <record>
                     |  <type-alias>
                     |  <algebraic-type>
                     |  <function>
                     |  <let>;
<open>              ::= "open" "(" [<id> {"," <id>}] ")";
<export>            ::= "export" "(" [<id> {"," <id>}] ")";
<template-dec>      ::= <id> {"," <id>} [";" <id> {"," <id>}];
<template-apply>    ::= <ty-expr> {"," <ty-expr>} [";" <capacity-expr> {"," <capacity-expr>}];
<record>            ::= "type" <id> ["<" <template-dec> ">"] "=" "{" [<ty-expr> <id> {";" <ty-expr> <id>}] "}";
<type-alias>        ::= "type" <id> ["<" <template-dec> ">"] "=" <ty-expr>;
<algebraic-type>    ::= "type" <id> ["<" <template-dec> ">"] "=" <value-constructor> {"|" <value-constructor>};
<value-constructor> ::= <id> "(" [<ty-expr> {, <ty-expr>}] ")";
<let>               ::= "let" <ty-expr> <id> "=" <expr>;
<function>          ::= "fun" <ty-expr> <id> ["<" <template-dec> ">"] "(" [<ty-expr> <id> {"," <ty-expr> <id>}] ")" "=" <expr>;
<declaration-ref>   ::= <id> | <module-qualifier>;
<ty-expr>           ::= <declaration-ref> ["<" <template-apply> ">"]
                     |  "(" [<ty-expr> {"," <ty-expr>}] ")" "->" <ty-expr>
                     |  <ty-expr> "[" <capacity-expr> "]";
<capacity-expr>     ::= <capacity-expr> <capacity-op> <capacity-expr>
                     |  <id>
                     |  <natural-number>;
<capacity-op>       ::= "+" | "-" | "*" | "/";
<module-qualifier>  ::= <id> ":" <id>;
<expr-list>         ::= <expr> {"," <expr>};
<field-assign-list> ::= <id> "=" <expr> {";" <id> "=" <expr>};
<expr>              ::= "()" | "true" | "false" | <number>                                               // X
                     |  "(" <expr> {";" <expr>} ")"                                                      // X
                     |  <expr> "(" [<expr-list>] ")"                                                     // X
                     |  <declaration-ref> "<" <template-apply> ">"
                     |  <expr> "[" <expr> "]"                                                            // X
                     |  <expr> <binary-op> <expr>                                                        // X
                     |  "if" <expr> "then" <expr> [{"elif" <expr> "then" <expr>}] ["else" <expr>] "end"  // X
                     |  "let" ["mutable"] [<ty-expr>] <id> "=" <expr>                                    // X
                     |  "set" <left-assign> "=" <expr>                                                   // X
                     |  "for" <ty-expr> <id> "in" <expr> ".." <expr> "do" <expr> "end"                   // X
                     |  "do" <expr> "while" <expr> "end"                                                 // X
                     |  "while" <expr> "do" <expr> "end"                                                 // X
                     |  <module-qualifier>
                     |  <id>                                                                             // X
                     |  "not" <expr>                                                                     // X
                     |  "~~~" <expr>                                                                     // X
                     |  <expr> "." <id>
                     |  "fn" <ty-expr> "(" [<ty-expr> {, <ty-expr>}] ")" "->" <expr>                     // X
                     |  "case" <expr> "of" <case-clause> {"|" <case-clause>} "end"
                     |  <declaration-ref> ["<" <template-apply> ">"] "{" [<field-assign-list>] "}"       // X
                     |  "[" <expr-list> "]";                                                             // X
<binary-op>         ::= "+" | "-" | "*" | "/" | "mod" | "and" | "or" | "&&&"
                     |  "|||" | ">=" | "<=" | ">" | "<" | "==" | "!=";
<left-assign>       ::= <id>
                     |  <left-assign> "[" <expr> "]"
                     |  <left-assign> "." <id>;
<case-clause>       ::= <pattern> "=>" <expr>;
<pattern>           ::= <id>
                     |  <integer>
                     |  <float>
                     |  "_"
                     |  <declaration-ref> "(" [<pattern> {"," <pattern>}] ")"
                     |  <declaration-ref> "{" [<id> "=" <pattern> {"," <id> "=" <pattern>}] "}";
