module Color
open(Prelude)

alias rgb = { r : uint8; g : uint8; b : uint8 }
alias rgba = { r : uint8; g : uint8; b : uint8; a : uint8 }
alias hsv = { h : float; s : float; v : float }
alias hsva = { h : float; s : float; v : float; a : float }

fun hsvToRgb(color : hsv) : rgb = (
    let {h=h; s=s; v=v} = color;
    let c : float = v * s;
    let x : float = c * toFloat(1.0 - Math:fabs_(Math:fmod_((toDouble(h) / 60), 2.0) - 1.0));
    let m : float = v - c;
    let (rPrime, gPrime, bPrime) =
        if 0.0f <= h and h < 60.0f then
            (c, x, 0.0f)
        elif 60.0f <= h and h < 120.0f then
            (x, c, 0.0f)
        elif 120.0f <= h and h < 180.0f then
            (0.0f, c, x)
        elif 180.0f <= h and h < 240.0f then
            (0.0f, x, c)
        elif 240.0f <= h and h < 300.0f then
            (x, 0.0f, c)
        else
            (c, 0.0f, x)
        end;
    let r = (rPrime + m) * 255.0f;
    let g = (gPrime + m) * 255.0f;
    let b = (bPrime + m) * 255.0f;
    {r=toUInt8(r); g=toUInt8(g); b=toUInt8(b)}
)

fun rgbToRgb565(color : rgb) : uint16 = (
    let {r=r; g=g; b=b} = color;
    ((u8ToU16(r) &&& 0xf8u16) <<< 8u32) ||| ((u8ToU16(g) &&& 0xfcu16) <<< 3u32) ||| (u8ToU16(b) >>> 3u32)
)

let red : rgb = {r=255u8; g=0; b=0}
let green : rgb = {r=0; g=255u8; b=0}
let blue : rgb = {r=0; g=0; b=255u8}
let black : rgb = {r=0; g=0; b=0}
let white : rgb = {r=255u8; g=255u8; b=255u8}
let yellow : rgb = {r=255u8; g=255u8; b=0}
let magenta : rgb = {r=255u8; g=0; b=255u8}
let cyan : rgb = {r=0; g=255u8; b=255u8}