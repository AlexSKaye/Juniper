%{

open Microsoft.FSharp.Text.Parsing

let pos (parseState: IParseState) startIdx endIdx =
    (parseState.InputStartPosition startIdx, parseState.InputEndPosition endIdx)

%}

%start start

// Calling on the tokens that were defined in the lexer
%token <string> INT
%token <string> FLOAT
%token <string> ID
%token <string> STRING
%token <string> INLINECODE
%token ALIAS
%token RECORD
%token RETURN
%token END
%token OPEN
%token EXPORT
%token MODULE
%token FUN
%token FN
%token IF
%token THEN
%token ELSE
%token ELIF
%token NULL
%token WHILE
%token DO
%token LET
%token MUTABLE
%token FOR
%token IN
%token TO
%token DOWNTO
%token LPAREN
%token RPAREN
%token LBRACK
%token RBRACK
%token LCURLYBRACE
%token RCURLYBRACE
%token COMMA
%token MOD
%token PLUS
%token MINUS
%token TIMES
%token DIVIDE
%token NOT
%token AND
%token OR
%token AMPERSAND
%token PIPE
%token TICK
%token TILDE
%token CASE
%token OF
%token TYPE
%token COLON
%token SEMICOLON
%token NOTEQUAL
%token DOUBLEEQUAL
%token EQUAL
%token GTHAN
%token LTHAN
%token GTHANEQ
%token ARRAY
%token LTHANEQ
%token UNIT
%token TRUE
%token FALSE
%token UNDERSCORE
%token DOT
%token SET
%token RFATARROW
%token REF
%token BSHIFTL
%token BSHIFTR
%token DEREF
%token EOF

%type < Module > start

// left means to shift the next symbol onto the stack (to define priorities and avoid shift-reduce errors)
%left SEMICOLON COMMA
%left EQUAL DOUBLEEQUAL NOTEQUAL
%left OR AND
%left UNDERSCORE
%left ID
%left BSHIFTL BSHIFTR
%left LTHAN LTHANEQ GTHAN GTHANEQ
%left PLUS MINUS PIPE
%left TIMES DIVIDE MOD AMPERSAND
%left REF
%left DEREF
%left NOT TILDE
%left DOT
%left LBRACK
%left LPAREN RPAREN

%%

// Entry point for the parser
start:
  | declarationList { Module ($1) }

declarationList:
  | declaration declarationList  { $1 :: $2 }
  |                              { [] }

declaration:
  | moduleNameDec   { (pos parseState 1 1, None, $1) }
  | exports         { (pos parseState 1 1, None, $1) }
  | fundec          { (pos parseState 1 1, None, $1) }
  | recorddec       { (pos parseState 1 1, None, $1) }
  | uniondec        { (pos parseState 1 1, None, $1) }
  | letdec          { (pos parseState 1 1, None, $1) }
  | opendec         { (pos parseState 1 1, None, $1) }

// Open a everything exported to a namespace from a module
opendec:
  | OPEN LPAREN idList RPAREN { OpenDec (pos parseState 3 3, None, $3) }

letdec:
  | LET ID COLON tyexpr EQUAL expr {
        LetDec {
            varName = (pos parseState 2 2, None, $2);
            typ = (pos parseState 4 4, None, $4);
            right = (pos parseState 6 6, None, $6) }}

// Export statements for Modules (so that other programs can use functions and types declared in them)
exports:
  | EXPORT LPAREN idList RPAREN { ExportDec (pos parseState 3 3, None, $3) }

moduleNameDec:
  | MODULE ID             { ModuleNameDec (pos parseState 2 2, None, $2) }

// Function declaration
fundec:
  | FUN ID LPAREN funargList RPAREN COLON tyexpr EQUAL expr {
        FunctionDec {
             name=(pos parseState 2 2, None, $2);
             template=None;
             clause=(pos parseState 3 9, None, {
                 arguments=(pos parseState 4 4, None, $4);
                 body=(pos parseState 9 9, None, $9);
                 returnTy=(pos parseState 7 7, None, $7)})}}

  | FUN ID LTHAN tycapvars GTHAN LPAREN funargList RPAREN COLON tyexpr EQUAL expr {
        FunctionDec {
             name = (pos parseState 2 2, None, $2);
             template = Some (pos parseState 4 4, None, $4);
             clause = (pos parseState 6 12, None, {
                arguments = (pos parseState 7 7, None, $7);
                body = (pos parseState 12 12, None, $12);
                returnTy = (pos parseState 10 10, None, $10)})}}

// List of function arguments
funargList:
  | ID COLON tyexpr COMMA funargList  {((pos parseState 3 3, None, $3), (pos parseState 1 1, None, $1)) :: $5}
  | ID COLON tyexpr                   {[((pos parseState 3 3, None, $3), (pos parseState 1 1, None, $1))]}
  |                                   {[]}

// Type capacity variables within templates
tycapvars:
  | tyVarList SEMICOLON idList  {{tyVars=(pos parseState 1 1, None, $1); capVars=(pos parseState 3 3, None, $3)} }
  | tyVarList                   { {tyVars=(pos parseState 1 1, None, $1); capVars=(pos parseState 1 1, None, [])} }

// List of type variables
tyVarList:
  | TICK ID COMMA tyVarList {(pos parseState 1 2, None, $2) :: $4}
  | TICK ID                 {[(pos parseState 1 2, None, $2)]}
  |                         {[]}

// An ID is a sequence of characters serving as things such as variable identifiers, type capacities, and other lists
idList:
  | ID COMMA idList         { (pos parseState 1 1, None, $1)::$3 }
  | ID                      { [(pos parseState 1 1, None, $1)] }
  |                         { [] }

// Type capacity expressions within templates
tycapExprs:
  | tyExprs SEMICOLON capExprs {{tyExprs=(pos parseState 1 1, None, $1); capExprs=(pos parseState 3 3, None, $3)}}
  | tyExprs                    {{tyExprs=(pos parseState 1 1, None, $1); capExprs=(pos parseState 1 1, None, [])}}

// List of type expressions
tyExprs:
  | tyexpr COMMA tyExprs {(pos parseState 1 1, None, $1)::$3}
  | tyexpr               {[(pos parseState 1 1, None, $1)]}
  |                      { [] }

// List of capacity expressions
capExprs:
  | capexpr COMMA capExprs  { (pos parseState 1 1, None, $1)::$3 }
  | capexpr                 { [(pos parseState 1 1, None, $1)] }
  |                         { [] }

// List of record declarations.
recorddec:
  | TYPE ID EQUAL LCURLYBRACE recordDecList RCURLYBRACE {
        RecordDec {
            name =     (pos parseState 2 2, None, $2);
            fields =   (pos parseState 5 5, None, $5);
            template = None}}
  | TYPE ID LTHAN tycapvars GTHAN EQUAL LCURLYBRACE recordDecList RCURLYBRACE {
        RecordDec {
            name =     (pos parseState 2 2, None, $2);
            fields =   (pos parseState 8 8, None, $8);
            template = Some (pos parseState 4 4, None, $4)}}


recordDecList:
  | ID COLON tyexpr SEMICOLON recordDecList  {($3, $1) :: $5}
  | ID COLON tyexpr                          {[($3, $1)]}
  |                                          {[]}

uniondec:
  | TYPE ID EQUAL valueConList                       {
        UnionDec {
            name =     (pos parseState 2 2, None, $2);
            valCons =  (pos parseState 4 4, None, $4);
            template = None}}
  | TYPE ID LTHAN tycapvars GTHAN EQUAL valueConList {
        UnionDec {
            name =     (pos parseState 2 2, None, $2);
            valCons =  (pos parseState 7 7, None, $7);
            template = Some (pos parseState 4 4, None, $4)}}

// Value constructor list
valueConList:
  | valueCon PIPE valueConList {$1 :: $3}
  | valueCon                   {[$1]}
  |                            {[]}

valueCon:
  | ID OF tyexpr {((pos parseState 1 1, None, $1), Some (pos parseState 3 3, None, $3))}
  | ID           {((pos parseState 1 1, None, $1), None)}

// Sequence (set of expressions separated by semicolons) expression list
seqExprList:
  | expr SEMICOLON seqExprList {(pos parseState 1 1, None, $1)::$3}
  | expr                       {[(pos parseState 1 1, None, $1)]}
  |                            {[]}

// List of expressions in an array
arrayExprList:
  | expr COMMA arrayExprList {(pos parseState 1 1, None, $1)::$3}
  | expr                     {[(pos parseState 1 1, None, $1)]}
  |                          {[]}

// Record initialization list
recordInitList:
  | ID EQUAL expr SEMICOLON recordInitList { ((pos parseState 1 1, None, $1),(pos parseState 3 3, None, $3))::$5 }
  | ID EQUAL expr                          { [((pos parseState 1 1, None, $1),(pos parseState 3 3, None, $3))] }
  |                                        { [] }

expr:
  | NULL {
        NullExp (pos parseState 1 1, None, ())}
  | INLINECODE {
        let str = $1
        let code = str.[1..String.length str - 2]
        InlineCode (pos parseState 1 1, None, code) }
  | ARRAY tyexpr OF expr END {
        ArrayMakeExp {
            typ = (pos parseState 2 2, None, $2)
            initializer = (pos parseState 4 4, None, $4)}}
  // Calling a function with no templates
  | expr LPAREN callExprList RPAREN {
        CallExp {
            func = (pos parseState 1 1, None, $1);
            args = (pos parseState 3 3, None, $3)}}
  // Refering to a function in the same module with a template  
  | ID LTHAN tycapExprs GTHAN {
        TemplateApplyExp {
            func = (pos parseState 1 1, None, VarExp { name = (pos parseState 1 1, None, $1) });
            templateArgs = (pos parseState 3 3, None, $3)} }
  // Refering to a function in another module with a template
  | ID COLON ID LTHAN tycapExprs GTHAN {
        TemplateApplyExp {
            func = (pos parseState 1 1, None, ModQualifierExp {module_=(pos parseState 1 1, None, $1); name=(pos parseState 3 3, None, $3)});
            templateArgs = (pos parseState 5 5, None, $5)} }
  // Accessing an array element
  | expr LBRACK expr RBRACK {
        ArrayAccessExp {
            array = (pos parseState 1 1, None, $1);
            index = (pos parseState 3 3, None, $3)}}
  // Sequence of expressions separated by semicolons
  | LPAREN seqExprList RPAREN         {
        SequenceExp (pos parseState 2 2, None, $2)}
  // If statement with no else
  | IF expr THEN expr END {
        IfElseExp {
            condition = (pos parseState 2 2, None, $2);
            trueBranch = (pos parseState 4 4, None, $4);
            falseBranch = (pos parseState 5 5, None, UnitExp (pos parseState 5 5, None, ()))}}
  // If statement with elifs and/or else statements
  | IF expr THEN expr elifList {
        IfElseExp {
            condition = (pos parseState 2 2, None, $2);
            trueBranch = (pos parseState 4 4, None, $4);
            falseBranch = (pos parseState 5 5, None, $5)}}
  | LET pattern EQUAL expr {
        LetExp {
            left = (pos parseState 2 2, None, $2);
            right = (pos parseState 4 4, None, $4)} }
  // Set expression for mutating a variable
  | SET leftAssign EQUAL expr {
        AssignExp {
            left = (pos parseState 2 2, None, $2);
            right = (pos parseState 4 4, None, $4);
            ref = (pos parseState 1 1, None, false) } }
  | SET REF leftAssign EQUAL expr {
        AssignExp {
            left = (pos parseState 3 3, None, $3);
            right = (pos parseState 5 5, None, $5);
            ref = (pos parseState 2 2, None, true)}}
  // For loop
  | FOR ID COLON tyexpr IN expr TO expr DO expr END {
        ForLoopExp {
            typ = (pos parseState 4 4, None, $4);
            varName = (pos parseState 2 2, None, $2);
            start = (pos parseState 6 6, None, $6);
            direction = (pos parseState 7 7, None, Upto)
            end_ = (pos parseState 8 8, None, $8);
            body = (pos parseState 10 10, None, $10)
        }
  }
  // For loop, downward counter
  | FOR ID COLON tyexpr IN expr DOWNTO expr DO expr END {
        ForLoopExp {
            typ = (pos parseState 4 4, None, $4);
            varName = (pos parseState 2 2, None, $2);
            start = (pos parseState 6 6, None, $6);
            direction = (pos parseState 7 7, None, Downto)
            end_ = (pos parseState 8 8, None, $8);
            body = (pos parseState 10 10, None, $10)
        }
  }
  // Do while loop
  | DO expr WHILE expr END {
        DoWhileLoopExp {
            condition = (pos parseState 4 4, None, $4);
            body = (pos parseState 2 2, None, $2) }}
  // While loop
  | WHILE expr DO expr END {
        WhileLoopExp {
            condition = (pos parseState 2 2, None, $2);
            body = (pos parseState 4 4, None, $4) }}
  | moduleQualifier                   {ModQualifierExp $1}
  | NOT expr                          {UnaryOpExp {op = (pos parseState 1 1, None, LogicalNot); exp = (pos parseState 2 2, None, $2)}}
  | TILDE TILDE TILDE expr            {UnaryOpExp {op = (pos parseState 1 3, None, BitwiseNot); exp = (pos parseState 4 4, None, $4)}}
  | UNIT                              {UnitExp (pos parseState 1 1, None, ())}
  | TRUE                              {TrueExp (pos parseState 1 1, None, ())}
  | FALSE                             {FalseExp (pos parseState 1 1, None, ())}
  // Record access
  | expr DOT ID {
        RecordAccessExp {
            record = (pos parseState 1 1, None, $1);
            fieldName = (pos parseState 3 3, None, $3)}}
	// Expressions resulting from binary operations
  | expr PLUS expr                          {BinaryOpExp {op = (pos parseState 2 2, None, Add); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr MINUS expr                         {BinaryOpExp {op = (pos parseState 2 2, None, Subtract); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr TIMES expr                         {BinaryOpExp {op = (pos parseState 2 2, None, Multiply); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr DIVIDE expr                        {BinaryOpExp {op = (pos parseState 2 2, None, Divide); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr MOD expr                           {BinaryOpExp {op = (pos parseState 2 2, None, Modulo); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr AND expr                           {BinaryOpExp {op = (pos parseState 2 2, None, LogicalAnd); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr OR expr                            {BinaryOpExp {op = (pos parseState 2 2, None, LogicalOr); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr AMPERSAND AMPERSAND AMPERSAND expr {BinaryOpExp {op = (pos parseState 2 2, None, BitwiseAnd); left=(pos parseState 1 1, None, $1); right=(pos parseState 5 5, None, $5)}}
  | expr PIPE PIPE PIPE expr                {BinaryOpExp {op = (pos parseState 2 2, None, BitwiseOr); left=(pos parseState 1 1, None, $1); right=(pos parseState 5 5, None, $5)}}
  | expr GTHANEQ expr                       {BinaryOpExp {op = (pos parseState 2 2, None, GreaterOrEqual); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr LTHANEQ expr                       {BinaryOpExp {op = (pos parseState 2 2, None, LessOrEqual); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr GTHAN expr                         {BinaryOpExp {op = (pos parseState 2 2, None, Greater); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr LTHAN expr                         {BinaryOpExp {op = (pos parseState 2 2, None, Less); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr DOUBLEEQUAL expr                   {BinaryOpExp {op = (pos parseState 2 2, None, Equal); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr NOTEQUAL expr                      {BinaryOpExp {op = (pos parseState 2 2, None, NotEqual); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr BSHIFTL expr                       {BinaryOpExp {op = (pos parseState 2 2, None, BitshiftLeft); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | expr BSHIFTR expr                       {BinaryOpExp {op = (pos parseState 2 2, None, BitshiftRight); left=(pos parseState 1 1, None, $1); right=(pos parseState 3 3, None, $3)}}
  | FN LPAREN funargList RPAREN COLON tyexpr MINUS GTHAN expr {
        LambdaExp {
            clause = (pos parseState 2 9, None, {
                arguments = (pos parseState 3 3, None, $3);
                body = (pos parseState 9 9, None, $9);
                returnTy = (pos parseState 6 6, None, $6)})}}
  | ID                                { VarExp { name = (pos parseState 1 1, None, $1) }}
  | INT                               { IntExp (pos parseState 1 1, None, $1) }
  | FLOAT                             { FloatExp (pos parseState 1 1, None, $1) }
  // Pattern matching
  | CASE expr OF caseClauseList END   {
        CaseExp {
            on = (pos parseState 2 2, None, $2);
            clauses = (pos parseState 4 4, None, $4)}}
  | ID LCURLYBRACE recordInitList RCURLYBRACE {
        RecordExp {
            recordTy = (pos parseState 1 1, None, TyName (pos parseState 1 1, None, $1));
            initFields = (pos parseState 3 3, None, $3);
            templateArgs=None } }
  | ID COLON ID LCURLYBRACE recordInitList RCURLYBRACE {
        RecordExp {
            recordTy = (pos parseState 1 3, None, TyModuleQualifier{module_=(pos parseState 1 1, None, $1); name=(pos parseState 3 3, None, $3)});
            initFields = (pos parseState 5 5, None, $5);
            templateArgs=None} }
  | ID LTHAN tycapExprs GTHAN LCURLYBRACE recordInitList RCURLYBRACE {
        RecordExp {
            recordTy = (pos parseState 1 1, None, TyName (pos parseState 1 1, None, $1));
            initFields = (pos parseState 6 6, None, $6);
            templateArgs = Some (pos parseState 3 3, None, $3) } }
  | ID COLON ID LTHAN tycapExprs GTHAN LCURLYBRACE recordInitList RCURLYBRACE {
        RecordExp { 
            recordTy = (pos parseState 1 3, None, TyModuleQualifier{
                module_ = (pos parseState 1 1, None, $1);
                name = (pos parseState 3 3, None, $3)});
            initFields = (pos parseState 8 8, None, $8);
            templateArgs = Some (pos parseState 5 5, None, $5)} }
  | LBRACK arrayExprList RBRACK { ArrayLitExp (pos parseState 1 3, None, $2) }
  | REF expr { RefExp (pos parseState 2 2, None, $2) }
  | DEREF expr { DerefExp (pos parseState 2 2, None, $2) }
  | LPAREN tupleExprList RPAREN { TupleExp $2 }

// List of tuples
tupleExprList:
  | expr COMMA tupleExprList {(pos parseState 1 1, None, $1)::$3}
  | expr COMMA expr          {[(pos parseState 1 1, None, $1); (pos parseState 3 3, None, $3)]}

// List of case clauses used for pattern matching case clauses
caseClauseList:
  | PIPE pattern RFATARROW expr caseClauseList {
        ((pos parseState 2 2, None, $2), (pos parseState 4 4, None, $4))::$5 }
  | PIPE pattern RFATARROW expr {
        [((pos parseState 2 2, None, $2), (pos parseState 4 4, None, $4))] }

// Pattern used within a case clause (to match against)
pattern:
  | INT                                              { MatchIntVal (pos parseState 1 1, None, $1)}
  | FLOAT                                            { MatchFloatVal (pos parseState 1 1, None, $1)}
  | ID {
        MatchVar { varName = (pos parseState 1 1, None, $1);
                   mutable_ = (pos parseState 1 1, None, false);
                   typ = None}}
  | ID COLON tyexpr {
        MatchVar { varName = (pos parseState 1 1, None, $1);
                   mutable_ = (pos parseState 1 1, None, false);
                   typ = Some (pos parseState 3 3, None, $3)}}
  | MUTABLE ID {
        MatchVar { varName = (pos parseState 2 2, None, $2);
                   mutable_ = (pos parseState 1 1, None, true);
                   typ = None}}
  | MUTABLE ID COLON tyexpr {
        MatchVar { varName = (pos parseState 2 2, None, $2);
                   mutable_ = (pos parseState 1 1, None, true);
                   typ = Some (pos parseState 4 4, None, $4)}}
  | UNDERSCORE { MatchUnderscore }
  | ID LPAREN pattern RPAREN {
        MatchValCon { name = (pos parseState 1 1, None, $1);
                      template = None;
                      innerPattern = (pos parseState 3 3, None, $3);
                      id = None} }
  | moduleQualifier LPAREN pattern RPAREN  {
        MatchValConModQualifier { modQualifier = (pos parseState 1 1, None, $1);
                                  template = None;
                                  innerPattern = (pos parseState 3 3, None, $3);
                                  id = None}}
  | ID LTHAN tycapExprs GTHAN LPAREN pattern RPAREN {
        MatchValCon { name = (pos parseState 1 1, None, $1);
                      template = Some (pos parseState 3 3, None, $3);
                      innerPattern = (pos parseState 6 6, None, $6);
                      id = None} }
  | moduleQualifier LTHAN tycapExprs GTHAN LPAREN pattern RPAREN {
        MatchValConModQualifier { modQualifier = (pos parseState 1 1, None, $1);
                                  template = Some (pos parseState 3 3, None, $3);
                                  innerPattern = (pos parseState 6 6, None, $6);
                                  id = None} }
  | moduleQualifier LCURLYBRACE recPatternList RCURLYBRACE {
        MatchRecCon { typ = (pos parseState 1 1, None, TyModuleQualifier $1);
                      fields = (pos parseState 3 3, None, $3)}}
  | ID LCURLYBRACE recPatternList RCURLYBRACE {
        MatchRecCon { typ = (pos parseState 1 1, None, TyName (pos parseState 1 1, None, $1));
                      fields  =(pos parseState 3 3, None, $3)}}
  | moduleQualifier LTHAN tycapExprs GTHAN LCURLYBRACE recPatternList RCURLYBRACE {
        let ty = TyApply {
                    tyConstructor = (pos parseState 1 1, None, TyModuleQualifier $1);
                    args = (pos parseState 3 3, None, $3)}
        MatchRecCon { typ = (pos parseState 1 4, None, ty);
                      fields = (pos parseState 6 6, None, $6)}}
  | ID LTHAN tycapExprs GTHAN LCURLYBRACE recPatternList RCURLYBRACE {
        let ty = TyApply {
                    tyConstructor = (pos parseState 1 1, None, TyName (pos parseState 1 1, None, $1));
                    args = (pos parseState 3 3, None, $3)}
        MatchRecCon { typ = (pos parseState 1 4, None, ty);
                      fields = (pos parseState 6 6, None, $6)}}
  | LPAREN tuplePatternList RPAREN { MatchTuple (pos parseState 2 2, None, $2) }
  |                                                  { MatchEmpty }

tuplePatternList:
  | pattern COMMA tuplePatternList  {(pos parseState 1 1, None, $1) :: $3}
  | pattern COMMA pattern           {[(pos parseState 1 1, None, $1); (pos parseState 3 3, None, $3)]}

recPatternList:
  | ID EQUAL pattern SEMICOLON recPatternList { ((pos parseState 1 1, None, $1), (pos parseState 3 3, None, $3)) :: $5}
  | ID EQUAL pattern                          { [((pos parseState 1 1, None, $1), (pos parseState 3 3, None, $3))]}
  |                                           {[]}

// List of elifs and elses used after an if
elifList:
  | ELSE expr END {$2}
  | ELIF expr THEN expr END {
        IfElseExp {
            condition = (pos parseState 2 2, None, $2);
            trueBranch = (pos parseState 4 4, None, $4);
            falseBranch = (pos parseState 5 5, None, UnitExp (pos parseState 5 5, None, ()))}}
  | ELIF expr THEN expr elifList {
        IfElseExp {
            condition = (pos parseState 2 2, None, $2);
            trueBranch = (pos parseState 4 4, None, $4);
            falseBranch = (pos parseState 5 5, None, $5) }}

// Call expression list - when you call a function, the list of arguments you pass it
callExprList:
  | expr COMMA callExprList {(pos parseState 1 1, None, $1)::$3}
  | expr                    {[(pos parseState 1 1, None, $1)]}
  |                         {[]}  

// Type expression
tyexpr:
  | moduleQualifier LTHAN tycapExprs GTHAN {
        TyApply {
            tyConstructor = (pos parseState 1 1, None, TyModuleQualifier $1);
            args = (pos parseState 3 3, None, $3)}}
  | ID LTHAN tycapExprs GTHAN {
        TyApply {
            tyConstructor = (pos parseState 1 1, None, TyName (pos parseState 1 1, None, $1));
            args = (pos parseState 3 3, None, $3)}}
  | LPAREN tyexprList RPAREN MINUS GTHAN tyexpr {
        FunTy {
            template = None
            args = $2;
            source=None;
            returnType = (pos parseState 6 6, None, $6)}}
  | tyexpr LBRACK capexpr RBRACK {
        ArrayTy {
            valueType = (pos parseState 1 1, None, $1);
            capacity = (pos parseState 3 3, None, $3)}}
  | tyexpr REF {
        RefTy (pos parseState 1 1, None, $1) }
  | moduleQualifier LBRACK capexpr RBRACK {
        ArrayTy {
            valueType = (pos parseState 1 1, None, TyModuleQualifier $1);
            capacity = (pos parseState 3 3, None, $3)}}
  | LPAREN tupleTyList RPAREN {TupleTy $2 }
  | TICK ID { ForallTy (pos parseState 1 2, None, $2) }
  | ID { match $1 with
             | "uint8" -> BaseTy (pos parseState 1 1, None, TyUint8)
             | "uint16" -> BaseTy (pos parseState 1 1, None, TyUint16)
             | "uint32" -> BaseTy (pos parseState 1 1, None, TyUint32)
             | "uint64" -> BaseTy (pos parseState 1 1, None, TyUint64)
             | "int8" -> BaseTy (pos parseState 1 1, None, TyInt8)
             | "int16" -> BaseTy (pos parseState 1 1, None, TyInt16)
             | "int32" -> BaseTy (pos parseState 1 1, None, TyInt32)
             | "int64" -> BaseTy (pos parseState 1 1, None, TyInt64)
             | "bool" -> BaseTy (pos parseState 1 1, None, TyBool)
             | "unit" -> BaseTy (pos parseState 1 1, None, TyUnit)
             | "float" -> BaseTy (pos parseState 1 1, None, TyFloat)
             | "double" -> BaseTy (pos parseState 1 1, None, TyDouble)
             | "pointer" -> BaseTy (pos parseState 1 1, None, TyPointer)
             | _ -> TyName (pos parseState 1 1, None, $1) }

tupleTyList:
  | tyexpr TIMES tupleTyList {(pos parseState 1 1, None, $1)::$3}
  | tyexpr TIMES tyexpr      {[(pos parseState 1 1, None, $1); (pos parseState 3 3, None, $3)]}

nonEmptyTyExprList:
  | tyexpr COMMA nonEmptyTyExprList {(pos parseState 1 1, None, $1)::$3}
  | tyexpr                          {[(pos parseState 1 1, None, $1)]}

tyexprList:
  | tyexpr COMMA tyexprList {(pos parseState 1 1, None, $1)::$3}
  | tyexpr                  {[(pos parseState 1 1, None, $1)]}
  |                         {[]}

// Expression used for 
capexpr:
  | ID                              { CapacityNameExpr (pos parseState 1 1, None, $1)}
  | INT                             { CapacityConst (pos parseState 1 1, None, $1) }
  | LPAREN capexpr RPAREN { $2 }
  | capexpr PLUS capexpr {
        CapacityOp {
            left = (pos parseState 1 1, None, $1);
            op = (pos parseState 2 2, None, CAPPLUS);
            right = (pos parseState 3 3, None, $3)}}
  | capexpr MINUS capexpr {
        CapacityOp {
            left = (pos parseState 1 1, None, $1);
            op = (pos parseState 2 2, None, CAPMINUS);
            right = (pos parseState 3 3, None, $3)} }
  | capexpr DIVIDE capexpr {
        CapacityOp {
            left = (pos parseState 1 1, None, $1);
            op = (pos parseState 2 2, None, CAPDIVIDE);
            right = (pos parseState 3 3, None, $3)} }
  | capexpr TIMES  capexpr {
        CapacityOp {
            left = (pos parseState 1 1, None, $1);
            op=(pos parseState 2 2, None, CAPMULTIPLY);
            right = (pos parseState 3 3, None, $3)} }

moduleQualifier:
  | ID COLON ID {
        {module_=(pos parseState 1 1, None, $1); name=(pos parseState 3 3, None, $3)} }

// Left side of an assign (to an array, record, or variable)
leftAssign:
  | leftAssign LBRACK expr RBRACK { ArrayMutation { array = (pos parseState 1 1, None, $1); index = (pos parseState 3 3, None, $3) } }
  | leftAssign DOT ID             { RecordMutation { record = (pos parseState 1 1, None, $1); fieldName = (pos parseState 3 3, None, $3) } }
  | ID                            { VarMutation { varName = (pos parseState 1 1, None, $1) } }
  | moduleQualifier               { ModQualifierMutation { modQualifier = (pos parseState 1 1, None, $1) } }